export AWS_PROFILE := "linecard"
export AWS_REGION := "us-west-2"
export PATH := x'$PWD/bin:${PATH}'
scaffolds := "go node python ruby"

[private]
@default:
    just -f {{ justfile() }} --list

[private]
build:
    go build -o bin/self ../cmd/self/main.go

# Initialize all scaffolds
[private]
init:
    #! /usr/bin/env bash
    set -e
    for scaffold in {{ scaffolds }}; do
        echo "initializing $scaffold"
        self init $scaffold init/testing-$scaffold
        cp -r fixtures/bus init/testing-$scaffold/
    done

# Publish all scaffolds
[private]
publish:
    #! /usr/bin/env bash
    set -e
    for scaffold in {{ scaffolds }}; do
        echo "publishing $scaffold"
        self publish init/testing-$scaffold --ecr-login
    done

# Deploy all scaffolds
[private]
deploy:
    #! /usr/bin/env bash
    set -e
    echo "deploying all scaffolds"
    for scaffold in {{ scaffolds }}; do
        echo "deploying $scaffold"
        self deploy init/testing-$scaffold
    done
    rm -rf bin/*

# Destroy all scaffolds
[private]
destroy:
    #! /usr/bin/env bash
    for scaffold in {{ scaffolds }}; do
        echo "destroying $scaffold"
        self destroy init/testing-$scaffold
    done

# Clean up all scaffolds
[private]
clean:
    #! /usr/bin/env bash
    echo "cleaning up bin/ and init/"
    rm -rf init/*
    rm -rf bin/*

# Setup all scaffolds
setup: build init publish deploy

# Test all scaffolds
test:
    #! /usr/bin/env bash
    set -e
    for scaffold in {{ scaffolds }}; do
        FUNCTION_PATH=init/testing-$scaffold bundler exec -- rspec -f d
    done

# Ensure all scaffolds
ensure: deploy

# Teardown all scaffolds
teardown: destroy clean

# Show deployed scaffolds
show:
    self deployments