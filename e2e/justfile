export SELF_API_GATEWAY_ID := "vas86x7yjc"
export SELF_API_GATEWAY_AUTH_TYPE := "JWT"
export SELF_API_GATEWAY_AUTHORIZER_ID := "c0bsen"
export PATH := x'$PWD/bin:${PATH}'

[private]
default:
    just --list

[private]
init lang:
    self init {{ lang }} init/testing-{{ lang }}
    cp -r fixtures/bus init/testing-{{ lang }}/

[private]
publish lang:
    self publish init/testing-{{ lang }} --ecr-login

[private]
deploy lang:
    self deploy init/testing-{{ lang }}

[private]
test lang:
    FUNCTION_PATH=init/testing-{{lang}} bundler exec -- rspec -f d

[private]
destroy lang:
    self destroy init/testing-{{ lang }}

[private]
clean lang:
    rm -rf init/testing-{{ lang }}

[private]
build:
    go build -o bin/self ../cmd/self/main.go

# initialize, publish, deploy
up: build
    #! /usr/bin/env bash
    set -e
    for lang in ruby python node go; do
        just init $lang
        just publish $lang
        just deploy $lang
    done

# run infrastructure tests
smoke:
    #! /usr/bin/env bash
    set -e
    for lang in ruby python node go; do
        just test $lang
    done

# destroy and clean up
down:
    #! /usr/bin/env bash
    set -e
    for lang in ruby python node go; do
        just destroy $lang
        just clean $lang
    done
    rm -rf bin/*

